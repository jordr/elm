AC_INIT(Elm, 0.1)
AM_INIT_AUTOMAKE(1.7)
AM_CONFIG_HEADER(config.h)

dnl C++ Support
AC_PROG_CXX
AC_STDC_HEADERS
AC_HAVE_HEADERS(string.h)

dnl Dynamic Libraries
AC_LIBTOOL_DLOPEN
dnl AC_DISABLE_SHARED
AC_LIBLTDL_CONVENIENCE
AC_PROG_LIBTOOL
AC_CONFIG_SUBDIRS(libltdl)
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)

dnl ==== Building mode ====
AC_ARG_WITH(mode, [  --with-mode={dev,normal,final}	Activate the given build mode.],,with_mode=dev)
case $with_mode in
dev)
	CXXFLAGS="-g3"
	;;
normal)
	CXXFLAGS="-g"
	;;
final)
	CPPFLAGS="$CPPFLAGS -DNDEBUG"
	CXXFLAGS="-O2"
	;;
*)
	AC_MSG_ERROR([unknown mode $with_mode])
	;;
esac


dnl XML Support
ELM_CHECK_PKG_CONFIG(LIBXML, libxml, 2.0, ,
	ELM_CHECK_CONFIG(LIBXML, xml2, 2.0))
AC_SUBST(LIBXML_CFLAGS)
AC_SUBST(LIBXML_LIBS)
AM_CONDITIONAL(HAS_LIBXML, test "$HAS_LIBXML" = "yes")


dnl ELM_DEBUG_DEFS=-DELM_DEBUG_AVLTREE
AC_SUBST(ELM_DEBUG_DEFS)


dnl IS_SHARED condition
AS_IF([libtool --config | grep "dlopen_support=yes"],
	[elm_dlopen_support=yes])
AM_CONDITIONAL(IS_SHARED, test "$elm_dlopen_support" = yes)
AS_IF(test "$elm_dlopen_support" = yes,
	AC_DEFINE(IS_SHARED, [], [dlopen support]))


# Look for GDB
AC_PATH_PROG(GDB_PATH, gdb)
AS_IF(test -n "$GDB_PATH",
	AC_DEFINE_UNQUOTED(GDB_PATH, "$GDB_PATH", [Path to GDB]))


# Look for /proc filesystem
AC_CHECK_FILE([/proc], [
	HAVE_PROC_FS=yes
	AC_DEFINE(HAVE_PROC_FS, [1], [/proc filesystem available])
])


# Select the default crash control behaviour
CRASH_HANDLER=CrashHandler::DEFAULT
AS_IF(test -n "$GDB_PATH" -a "$HAVE_PROC_FS" = "yes",
	[
		have_gdb_crash_handler=yes
		AC_DEFINE(HAVE_GDB_CRASH_HANLDER, [1], [GDB crash handler availability])
		CRASH_HANDLER=GDBCrashHandler::DEFAULT
	])
AM_CONDITIONAL(HAVE_GDB_CRASH_HANDLER, test "$have_gdb_crash_handler" = "yes")
AC_DEFINE_UNQUOTED(CRASH_HANDLER, $CRASH_HANDLER, [Default crash handle])


AC_OUTPUT([
	Makefile
	src/Makefile
	test/Makefile
	include/Makefile
	include/elm/Makefile
	include/elm/alloc/Makefile
	include/elm/block/Makefile
	include/elm/checksum/Makefile
	include/elm/datastruct/Makefile
	include/elm/debug/Makefile
	include/elm/genstruct/Makefile
	include/elm/inhstruct/Makefile
	include/elm/io/Makefile
	include/elm/option/Makefile
	include/elm/serial/Makefile
	include/elm/serial2/Makefile
	include/elm/string/Makefile
	include/elm/system/Makefile
	include/elm/util/Makefile
	include/elm/xom/Makefile
], [])
